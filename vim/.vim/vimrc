" options ---

syntax on
filetype plugin indent on

let g:jellybeans_use_term_background_color = 1
" ignore if the scheme is missing, minpac will install it
silent! colorscheme jellybeans

set encoding=utf-8
set scrolloff=1
set hidden
set splitright
set nostartofline
set autoread
set mouse=

" Indentation
set nowrap
set tabstop=8
set shiftwidth=4
set softtabstop=4
set shiftround
set expandtab
set breakindent
set showbreak=\\\\\ \ 

" Searching
set incsearch
set ignorecase
set smartcase
set infercase
set hlsearch

" Status bar
set ruler
set laststatus=2
set showcmd
set wildmenu
set wildmode=longest:full,full

" set the complete char for mappings
set wildcharm=<c-z>

" allow backspacing over everything in insert mode
set backspace=indent,eol,start

" GUI setup
if has("gui_running")
  set guifont=Menlo:h16
  " Start without toolbar and scrollbars
  set guioptions=egmc
  " Turn off cursor blinking
  set guicursor+=a:blinkon0
  set columns=90
  set lines=52
  colorscheme jellybeans
endif

" save backups, undo and swaps in the same dir
let s:vim_data_dir = $HOME . '/.local/var/vim//'
let s:vim_undo_dir = s:vim_data_dir . 'undo//'

if !isdirectory(s:vim_undo_dir)
  call mkdir(s:vim_undo_dir, 'p')
endif

let &backupdir=s:vim_data_dir
let &directory=s:vim_data_dir
let &undodir=s:vim_undo_dir
set undofile

" netrw
let g:netrw_home = s:vim_data_dir

" gutentags
let g:gutentags_cache_dir = s:vim_data_dir . "tags"


" mappings ---

nnoremap j gj
nnoremap k gk
nnoremap gj j
nnoremap gk k

" files
cnoremap <expr> %% fnameescape(expand("%:p:h")."/")
nmap <space>e :edit %%<c-z>
nmap <space>f :find %%<c-z>**/*

" options
nnoremap <C-k> :setlocal wrap!<cr>

" buffers
noremap <space>- :b#<cr>
noremap <space>U :ls<cr>:b 
noremap <silent> <space>bd :call <SID>DeleteCurrentBuffer()<cr>

" fzf
nnoremap <space>o :Files<cr>
nnoremap <space>O :GFiles<cr>
nnoremap <space>u :Buffers<cr>
nnoremap <space>i :History<cr>

" nN always search in the same direction
noremap <expr> n 'Nn'[v:searchforward]
noremap <expr> N 'Nn'[!v:searchforward]

" change current word
nnoremap c* *Ncgn

" nicer command line history navigation
cnoremap <C-p> <up>
cnoremap <C-n> <down>

" jump to first non-whitespace on line, jump to begining of line if already at first non-whitespace
nnoremap <expr> <silent> 0 col('.') == match(getline('.'),'\S')+1 ? '0' : '^'

" GUI window zoom
nnoremap <space>wv :set lines=999<cr>
nnoremap <space>wz :set lines=999 <bar> set columns=999<cr>

" typos
cnoremap <expr> X (getcmdtype() is# ':' && empty(getcmdline())) ? 'x' : 'X'
cnoremap <expr> Q (getcmdtype() is# ':' && empty(getcmdline())) ? 'q' : 'Q'


" commands ---

command! -nargs=1 -complete=help Help call s:ShowHelp(<f-args>)


" functions ---

" delete buffer without closing the window
function! s:DeleteCurrentBuffer() abort
  let b = bufnr('%')
  if buflisted(bufnr('#'))
    execute 'buffer #'
  else
    execute 'bprev'
  endif
  execute 'bdelete ' . b
endfunction

" split help vertically if thereâ€™s room
function! s:ShowHelp(tag) abort
  if winheight(0) * 2 < winwidth(0)
    execute 'vertical help '.a:tag
  else
    execute 'help '.a:tag
  endif
endfunction
